Package compiler;

Helpers
    sp = ' ';
    lf = 0x000a;
    cr = 0x000d;
    ff = 0x000c;
    ht = 0x0009; 
    line_terminator = lf | cr | cr lf;
    white_space = (sp | ht | ff | line_terminator)*;

    all = [0..0xffff];
    input_character = [all - [cr + lf]];

    digit = ['0' .. '9'];

    hexdig = digit | ['a' .. 'f'] | ['A' .. 'F'];
    hexcode = hexdig hexdig;

    escape_seq = '\n' | '\t' | '\r' | '\0'  | '\\' | '\' ''' | '\"' | '\x' hexcode;

    latin_letter = ['a' .. 'z'] | ['A' .. 'Z']; 

    char_literal = ' ' | '!' | '#' | '$' | '%' | '&' | '(' | ')' 
        | '*' | '+' | ',' | '-' | '.' | '/' | ':' | ';' | '<' | '=' | '>' 
        | '?' | '@' | '[' | ']' | '^' | '_' | '`' | '{' | '|' | '}' | '~'
        | latin_letter | digit | escape_seq;

    not_dollar = [all - '$'];

    simple_comment = '$' (not_dollar input_character*)? line_terminator?;
    multi_comment = '$$' ('$'? not_dollar)* '$$';

Tokens
    white_space = white_space;
    escape_seq = escape_seq;
    comment = simple_comment | multi_comment;

    and     = 'and'; 
    char    = 'char';
    div     = 'div';
    do      = 'do';
    else    = 'else';
    fun     = 'fun';
    if      = 'if';
    int     = 'int';
    mod     = 'mod';
    not     = 'not';
    nothing = 'nothing';
    or      = 'or';
    ref     = 'ref';
    return  = 'return';
    then    = 'then';
    var     = 'var';
    while   = 'while';

    identifier = latin_letter (latin_letter | digit | '_' )*;

    integers = digit+;

    char_const = ''' char_literal ''';

    string_literal = '"' char_literal* '"';


    //operators

    minus   = '-';
    plus    = '+';
    star    = '*';
    neq     = '#';
    eq      = '=';
    lt      = '<';
    gt      = '>';
    lteq    = '<=';
    gteq    = '>=';

    //separators

    l_par   = '(';
    r_par   = ')';
    l_br    = '[';
    r_br    = ']';
    l_ag    = '{';
    r_ag    = '}';
    comma   = ',';
    semi    = ';';
    colon   = ':';
    arrow   = '<-';

Ignored Tokens
    white_space,
    comment;

Productions
    program = fun_def;

    fun_def =
        header local_def* block;

    header =
        fun identifier l_par fpar_def r_par colon return_type;

    fpar_def =
        {simple_par} ref? identifier colon types |
        {mult_par} ref? identifier comma fpar_def |
        {mult_types} ref? identifier colon types comma fpar_def |
        {semi_par} ref? identifier colon types semi fpar_def |
        {none} ;

    data_types =
        {int} int |
        {char} char;

    array_types =
        {brackets} l_br integers? r_br;

    types =
        {simple} data_types |
        {array} data_types array_types+;

    return_type =
        {simple} data_types |
        {none} nothing;

    local_def =
        {fun} fun_def |
        {dec} fun_dec |
        {var} var_def;

    fun_dec =
        header semi;

    block =
        l_ag stmt* r_ag;

    stmt =
        {semi} semi | 
        {assignment} assignment |   //<---- assignment
        {block} block | 
        {fun_cal} fun_cal semi |
        {ifstmt} ifstmt | 
        {whilestmt} whilestmt |
        {returnstmt} returnstmt;

    assignment =
        l_val arrow expr semi;

    l_val =
        {id} identifier | 
        {string} string_literal | 
        {id_brackets} l_val l_br expr r_br;  // Paizetai malakia string brackets??? l_val

    fun_cal =
        identifier l_par expr_list r_par;

    expr_list =
        {simple} expr |
        {list} expr_list comma expr |
        {none} ; 

    var_def =
        var var_ids colon types semi;

    var_ids =
        {id} identifier |
        {list} identifier comma var_ids;

    /* ----------------- IF & WHILE  ---------------------- */

    whilestmt =
        while cond do stmt ;

    while_with_else =
        while cond do stmt_with_else ;

    ifstmt =
        if_header if_trail ;

    if_header =
        if cond then;

    if_trail  =
        {no_else} [then]:stmt | 
        {with_else} [then]:stmt_with_else else [else_st]:stmt ;

    stmt_with_else =
        {semi} semi | 
        {assignment} assignment | 
        {stmt_returnstmt} returnstmt |
        {stmt_funcal} fun_cal semi |
        {block} block | 
        {if} if_else | 
        {while} while_with_else ;

    if_else =
        if_header [then]:stmt_with_else else [else_st]:stmt_with_else ;

    /* ---------------------------------------------------- */

    /* ----------------- CONDITION  ---------------------- */

    cond =
        {cond_and} conditional_and_expression |
        {or_expr} [left]:cond or [right]:conditional_and_expression;

    conditional_and_expression =
        {cond_not} conditional_not_expression |
        {and_expr} [left]:conditional_and_expression and [right]:comparative_expression;

    conditional_not_expression =
        {not_expr} not conditional_not_expression |
        {comparative} comparative_expression;

    comparative_expression =
        {relational} relational_expression |
        {equal} [left]:comparative_expression eq [right]:relational_expression |
        {not_equal} [left]:comparative_expression neq [right]:relational_expression ;

    relational_expression =
        {additive} expr |
        {less_than} [left]:relational_expression lt [right]:expr |
        {greater_than} [left]:relational_expression gt [right]:expr |
        {greater_equal_than} [left]:relational_expression gteq [right]:expr |
        {less_equal_than} [left]:relational_expression lteq [right]:expr ;

    /* ---------------------------------------------------- */

    returnstmt =
        {semi} return expr? semi;

    expr =
        {factor} factor |
        {add} expr plus factor |
        {sub} expr minus factor;

    factor =
        {term} term |
        {mult} factor star term |
        {mod} factor mod term |
        {div} factor div term;

    term =
        {plus_or_minus} plus_or_minus term |
        {int} integers |
        {char} char_const |
        {l_val} l_val |
        {fun_cal} fun_cal |
        {par} l_par expr r_par;

    plus_or_minus =
        {plus} plus | 
        {minus} minus;
